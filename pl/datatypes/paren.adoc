= Paren! - typ danych
:toc:
:numbered:

== Streszczenie

Datatype `paren!` jest natychmiast wykonaną wartością `block!`. Jest typu `series!` bez ograniczenia na typ danych, który może być użyty.

Wartości `paren!` są ograniczone okrągłymi nawiasami `(` i `)`.

Wykonanie może być zatrzymane przy użyciu `quote` przed wartością `paren!`. `quote (print 42)`

Wartości `paren!` jest członkiem danych typesetów (zbiór typów): `any-block!`, `any-list!`, `series!`

== Tworzenie

Wartości `paren` mogą być utworzone przy użyciu dosłownej składni, albo w czasie wykonywania przy użyciu konstruktora `make`, konwertera `to` lub funkcji natywnej `as`.

Aby utworzyć pustą wartość `paren!` wstępnie przydzielonym miejscem na 10 elementów:

```red
>> make paren! 10
== ()
```

Aby utworzyć wartość `paren!` z 10 elementami o wartości `none`:

```red
>> append/dup make paren! 10 none 10
== (none none none none none none none none none none)
```

Używając konwertera `to`:

```red
>> to paren! {one 2 "three" 4:00}
== (one 2 "three" 4:00:00)

>> to paren! "foo 1.23 me@me.com"
== (foo 1.23 me@me.com)

>> to paren! 42
== (42)
```

Używając `as`:

```red
>> as paren! [1 2 3]
== (1 2 3)
```

== Dosłowna składnia

Kolekcja wartości oddzielona przerwami ograniczona nawiasami okrągłymi.

`()`

`quote (one 2 "three")`

`(print 1.23)`

Wartości `paren` mogą rozciągać się przez wiele linii i zawierać inne wartości `paren!`.
```red
(1
2
"three"
)

quote (1 2 "three" (x foo 1.23))
```


Dodatkowe linie, spacje, tabulacje są dopuszczone, gdy nie łamią wartości.

Prawidłowo: `(print "foo")`

Nieprawidłowo: `(pr   int "foo")`


== Porównania

Wszystkie operatory porównań mogą być użyte z `paren!`: `=, ==, <>, >, <, >=, &lt;=, =?`. W dodatku `min` i `max` też są wspierane.

[NOTE]
Ponieważ wartości `paren!` są natychmiast wykonywane, zwrócona wartość z `min` albo `max` będzie ostatnim wyrażeniem z wartości `paren!`. Komparatory będą porównywać ostatnie elementy wartości `paren!` jeśli wykonywanie nie będzie wstrzymane.

```red
>> max (1 2 3) (4 5 6)
== 6
```

```red
>> (1 2 3) < (4 5 6)
== true

>> (1 2 3) < (4 5 2)
== false

>> (quote (1 2 3)) < (quote (4 5 2))
== true
```

== Testowanie wartości

Użyj `paren?`, aby sprawdzić czy wartość jest typu `paren!`.
```red
>> paren? quote (42 print "foo")
== true
```


Użyj `type?`, aby zwrócić typ danych danej wartości.
```red
>> type? quote (42)
== paren!
```

[NOTE, caption=Caution]

Wykonane wartości `paren!` zwrócą typ ostatniego wyrażenia.

```red
>> paren? (42)
== false

>> any-list? (42)
== false

>> type? (42)
== integer!

>> type? ('a 2 + 2 "hello")
== string!
```

== Poprzednio zdefiniowane słowa

=== Funkcje

`expand-directives`, `paren?`, `series?`, `to-paren`

=== Funkcje natywne

`as`, `compose`
